# Maintainer:   M.Reynolds <blackboxnetworkproject@gmail.com>
# Contributor:  Bjoern Franke <bjo at nord-west.org>
# Contributor:  flan_suse

pkgname=xfce-theme-greybird
pkgver=3.22.12
pkgrel=1
pkgdesc="A grey and blue Xfce theme."
arch=('any')
url="https://github.com/shimmerproject/Greybird"
license=('CCPL:by-sa-3.0' 'GPL')
groups=('xfce-themes-shimmer-collection')
conflicts=("$pkgname-git")
depends=('gtk-engine-murrine')
makedepends=('meson' 'sassc' 'librsvg')
optdepends=('elementary-xfce-icons: matching icon set; use the dark icon theme'
            'gtk3: required for CSS/GTK3 theme'
            'lightdm-gtk-greeter: required for the LightDM GTK theme'
            'lightdm-unity-greeter: required for the LightDM Unity theme'
            'shimmer-wallpapers: contains the Greybird wallpaper, among others'
            'lib32-gtk-engine-murrine: required for multilib')
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/shimmerproject/Greybird/archive/v$pkgver.tar.gz")
sha256sums=('410804cd5daca33cbc5c7c4a179f3a334ced87f408e515fc5d9c083a04bec4bc')

build() {
    cd Greybird-$pkgver
    meson --prefix=/usr build
    ninja -C build
}

package() {
    cd "$srcdir"/Greybird-$pkgver
    DESTDIR="$pkgdir" ninja -C build install
}
